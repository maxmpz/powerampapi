apply plugin: 'com.android.application'

android {
    compileSdk 31
    buildToolsVersion '31.0.0'

    defaultConfig {
        applicationId "com.poweramp.v3.sampleskin"
        minSdk 21
        //noinspection OldTargetApi
        targetSdk 31
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }

    lintOptions {
        disable "ResAuto", "MissingTranslation", "OnClick", "ExtraTranslation"

    }
    
    aaptOptions {
        def localProps = new Properties()
        localProps.load(rootProject.file("local.properties").newDataInputStream())
        def paResources = localProps['poweramp.apk'] ?: "Poweramp-build-935-resources.zip"
        println "Using $paResources PA resources"

        additionalParameters "--emit-ids", "${projectDir}/stable-ids.txt",
                                "--package-id", "0x80",
                                "-I", "${projectDir}/../${paResources}"
        if(project.file("stable-ids.txt").exists()) {
            println "Got stable-ids OK"
            additionalParameters "--stable-ids", "${projectDir}/stable-ids.txt"
        } else {
            println "WARNING: no stable-ids.txt specified"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat-resources:1.4.1"
}
