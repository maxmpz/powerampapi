plugins {
    alias(libs.plugins.agp.application)
    alias(libs.plugins.kotlin.android)
}

android {
    compileSdk COMPILE_SDK
    namespace "com.poweramp.v3.sampleskin"

    defaultConfig {
        minSdk MIN_SDK
        targetSdk TARGET_SDK
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility = JAVA_SOURCE_COMPATIBILITY
        targetCompatibility = JAVA_TARGET_COMPATIBILITY
    }
    lint {
        disable 'ResAuto', 'MissingTranslation', 'OnClick', 'ExtraTranslation'
    }

    androidResources {
        def localProps = new Properties()
        localProps.load(rootProject.file("local.properties").newDataInputStream())
        def paResources = localProps['poweramp.apk'] ?: "Poweramp-build-1003-resources.zip"
        println "Using $paResources PA resources"

        def params = []

        params += ["--emit-ids", "${projectDir}/stable-ids.txt", "--package-id", "0x80",  "-I", "${projectDir}/../${paResources}"]

        if(project.file("stable-ids.txt").exists()) {
            println "Got stable-ids OK"
            params += ["--stable-ids", "${projectDir}/stable-ids.txt"]
        } else {
            println "WARNING: no stable-ids.txt specified"
        }

        additionalParameters = params
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    implementation libs.androidx.appcompat.resources
    implementation libs.androidx.core.ktx
}
